
plugins {
    id 'com.android.application'
    id 'io.left.rightmesh.rightmesh-plugin' version '1.8.4'
    id "com.google.protobuf" version "0.8.7"
}

ext {
    artifactory_app_key = "0xa67d48cef4faaba71ce55435e389f5fdf4bb6419"
    artifactory_app_username = "$RightMeshUsername"
    artifactory_app_password =  "$RightMeshPassword"
}

rightmesh {
    appKey = "$artifactory_app_key"
    username = "$artifactory_app_username"
    password =  "$artifactory_app_password"
}

preBuild.dependsOn("rightmesh")

repositories {
    google()
    mavenCentral()
    jcenter()
    maven {
        url "https://artifactory.rightmesh.io/artifactory/maven"
        credentials {
            username artifactory_app_username
            password artifactory_app_password
        }
    }
    maven { url "https://dl.bintray.com/ethereum/maven" }
    maven { url "https://jitpack.io" }
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "ca.marshallasch.veil"
        minSdkVersion 18
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true //Allows usage of Using Vector Drawable Compat
    }
    dexOptions {
        javaMaxHeapSize "4g"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0-beta-1'
    }
    plugins {
        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
            }
            task.plugins {
                javalite {}
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])


    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:customtabs:28.0.0'
    implementation 'com.android.support:support-vector-drawable:28.0.0'
    implementation 'com.android.support:support-media-compat:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'

    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.android.support:design:28.0.0'


    implementation 'io.left.rightmesh:lib-rightmesh-android:0.10.0'
    implementation 'org.mindrot:jbcrypt:0.4'
    implementation 'com.chauthai.swipereveallayout:swipe-reveal-layout:1.4.1'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3'
    testImplementation "org.robolectric:robolectric:4.0-alpha-3"
}
